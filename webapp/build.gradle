/*
Vorgehen:
- OK Rest Add-On
- OK /pubsignal code
- OK /pubsignal test code
- OK /doc code
* */

buildscript {
    ext.cubaVersion = '7.2.19'
    repositories {
        maven {
            url 'https://repo.cuba-platform.com/content/groups/work'

            credentials {
                username(rootProject.hasProperty('repoUser') ? rootProject['repoUser'] : 'cuba')
                password(rootProject.hasProperty('repoPass') ? rootProject['repoPass'] : 'cuba123')
            }
        }


    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
    }
}

def modulePrefix = 'app'

def globalModule = project(":${modulePrefix}-global")
def coreModule = project(":${modulePrefix}-core")
def webModule = project(":${modulePrefix}-web")

def servletApi = 'javax.servlet:javax.servlet-api:3.1.0'

apply(plugin: 'cuba')

cuba {
    artifact {
        group = 'ch.so.agi.simi'
        version = getVersion(true)
        isSnapshot = false
    }
}



dependencies {
    appComponent("com.haulmont.cuba:cuba-global:$cubaVersion")
    appComponent('com.haulmont.addon.helium:helium-global:0.4.0')
    appComponent('com.haulmont.addon.restapi:restapi-global:7.2.4')
}

def postgres = 'org.postgresql:postgresql:42.2.9'
def hsql = 'org.hsqldb:hsqldb:2.7.1'

configure([globalModule, coreModule, webModule]) {
    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'cuba')

    dependencies {
        testCompile('org.junit.jupiter:junit-jupiter-api:5.5.2')
        testCompile('org.junit.jupiter:junit-jupiter-engine:5.5.2')
        testCompile('org.junit.vintage:junit-vintage-engine:5.5.2')

        compile('com.fasterxml.jackson.core:jackson-databind:2.13.4.2')
        compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.4')

        compile('io.github.sogis:meta2file:1.0.99'){
            exclude group: 'org.slf4j', module: 'slf4j-simple'
        }
    }
/*
    configurations.implementation {
        exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
        exclude(group: 'org.slf4j', module: 'slf4j-reload4j')
        exclude(group: 'org.slf4j', module: 'slf4j-simple')
    }*/

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    } 
    test {
        useJUnitPlatform()
    }
}


configure(globalModule) {
    dependencies {
        if (!JavaVersion.current().isJava8()) {
            runtime('javax.xml.bind:jaxb-api:2.3.1')
            runtime('org.glassfish.jaxb:jaxb-runtime:2.3.1')
        }
    }

    entitiesEnhancing {
        main {
            enabled = true
        }
    }
}

configure(coreModule) {

    configurations {
        jdbc
        dbscripts
    }

    dependencies {
        compile(globalModule)
        compileOnly(servletApi)
        jdbc(postgres)
        testRuntime(postgres)
        testRuntime(hsql)
    }

    task cleanConf(description: 'Cleans up conf directory', type: Delete) {
        delete "$cuba.appHome/${modulePrefix}-core/conf"
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}-core"
        appJars(modulePrefix + '-global', modulePrefix + '-core')
    }

    task dbViews(type: Copy, dependsOn: assembleDbScripts){

        from('../../schema/views/solr'){
            include{ isInitScript(it.name) }
            rename{ deferNameForInitScript(it, 90) }
        }
        from('../../schema/views/trafo'){
            include{ isInitScript(it.name) }
            rename{ deferNameForInitScript(it, 95) }
        }
        from('../../schema/views/app'){
            include{ isInitScript(it.name) }
            rename{ deferNameForInitScript(it, 99) }
        }

        into 'build/db/60-simi/init/postgres'
    }

    task createDb(dependsOn: dbViews, description: 'Creates local database', type: CubaDbCreation) {

    }

    task updateDb(dependsOn: assembleDbScripts, description: 'Updates local database', type: CubaDbUpdate) {
    }
}

configure(webModule) {
    configurations {
        webcontent
    }

    dependencies {
        compileOnly(servletApi)
        compile(globalModule)

        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2'
    }

    task webArchive(type: Zip) {
        from file("$buildDir/web")
        from file('web')
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }

    task deploySqlQueries(type: Copy) {
        from file('db_queries')
        into "$cuba.appHome/${modulePrefix}/conf"
    }

    task deployConf(dependsOn: deploySqlQueries, type: Copy) {
        from file('src')
        include "ch/so/agi/simi/**"
        into "$cuba.appHome/${modulePrefix}/conf"
    }

    task clearMessagesCache(type: CubaClearMessagesCache) {
        appName = "${modulePrefix}"
    }
    deployConf.dependsOn clearMessagesCache

    task cleanConf(description: 'Cleans up conf directory', type: Delete) {
        delete "$cuba.appHome/${modulePrefix}/conf"
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}"
        appJars(modulePrefix + '-global', modulePrefix + '-web')
    }

    task buildScssThemes(type: CubaWebScssThemeCreation)

    task deployThemes(type: CubaDeployThemeTask, dependsOn: buildScssThemes)

    assemble.dependsOn buildScssThemes

    task themesJar(type: Jar) {
        from file('themes')
        classifier = 'themes'
    }

    artifacts {
        archives themesJar
    }
}

task undeploy(type: Delete, dependsOn: ":${modulePrefix}-web:cleanConf") {
    delete("$cuba.tomcat.dir/shared")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}-core")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}")
}

task restart(dependsOn: ['stop', ":${modulePrefix}-core:deploy", ":${modulePrefix}-web:deploy"], description: 'Redeploys applications and restarts local Tomcat') {
    doLast {
        ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
            not {
                socket(server: 'localhost', port: '8787')
            }
        }
    }
}
restart.finalizedBy start

String getVersion(boolean useFullVersion){
    String version = "2.3"

    if(useFullVersion)
        version = version + "." + getBuildNumber()

    return version
}

String getBuildNumber() {
    String val = System.getenv('GITHUB_RUN_NUMBER')

    if(val == null)
        val = 'localbuild'

    return val
}

boolean isInitScript(String fileName){
    return java.util.regex.Pattern.matches("[0-9]+[_]{1}.*", fileName) // all files with numeric prefix
}

String deferNameForInitScript(String sourceName, int prefixNum){
    prefixNum + "." + sourceName.replace(".sql", "") +  ".create-db.sql"
}

